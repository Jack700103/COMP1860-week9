// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/Memory.hdl
/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load=1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the Screen
 * and Keyboard chip specifications given in the lectures and the book.
 */
CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
	
    RAM16K(in=in,address=address[0..13],load=loadRAM,out=RAMOut);
    Screen(in=in,address=address[0..12], load = loadScreen,out=ScreenOut);
    Keyboard(out=KeyOut1);

    //if address < 168384 then load/read RAM16K
    Not(in=address[14],out=not14);
    And(a=not14,b=load,out=loadRAM);

    //If address >= 16384  (100000000000000) and < 24576 (110000000000000) then use screen 
    //address[14]== 1 and address[13]==0
    Not(in=address[13],out=not13);
    And(a=address[14],b=not13,out=not1314);
    And(a=not1314,b=load,out=loadScreen);

    //If address 24576 (110000000000000) then get value from the keyboard
    //if address > 24576 the out 0

    
    Or8Way(in=address[0..7],out=or1);
    Or8Way(in[0..4]=address[8..12],in[5]=not13,in[6]=not14,in[7]=false,out=or2);
   
    Or(a=or1,b=or2,out=orall);
    Not(in=orall,out=readKey);
   
    Mux16(a=false,b=KeyOut1,sel=readKey,out=KeyOut2);
    
    Mux4Way16(a=RAMOut,b=RAMOut,c=ScreenOut,d=KeyOut2,sel=address[13..14],out=out);
   
}